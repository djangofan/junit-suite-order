apply plugin: 'java'
apply plugin: 'eclipse'
group = 'com.test'

ext {
    projTitle = 'Test JUnit Annotation Order'
    projVersion = '1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.+'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
}

tasks.withType(Test) {
    jvmArgs '-Xms128m', '-Xmx1024m', '-XX:MaxPermSize=128m'
    maxParallelForks = 8
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }
    forkEvery = 0
}

task runTestSuite(type: Test) {
    description = "Uses standard JUnit Test Suite sequential execution style."
    include '**/TestSuite.class'
    testReportDir = file("${reporting.baseDir}/TestSuite")
    testResultsDir = file("${buildDir}/test-results/TestSuite")
}

task runParallelSuite(type: Test) {
    description = "Uses experimental runner JUnit ParallelComputer execution style."
    include '**/ParallelSuite.class'
    testReportDir = file("${reporting.baseDir}/ParallelSuite")
    testResultsDir = file("${buildDir}/test-results/ParallelSuite")
}

task runSkipSuite(type: Test) {
    description = "Uses Gradle filter to execute targeted test group."
    include '**/XTest*.class'
    testReportDir = file("${reporting.baseDir}/XTest")
    testResultsDir = file("${buildDir}/test-results/XTest")
}

